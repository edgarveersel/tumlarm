/* Experimental ESP8266 piece of code
  *
  *  Start Date: 2018-09-07
  *  Coding: Edgar, Nick
  *
  *  Version: 0.1.0
  *  Version Date: 2018-10-1
  *
  * Capacitor issue: http://forum.arduino.cc/index.php?topic=394691.0
  *
  */

// The accuracy is 16-bits.

//****************************************
//        INCLUDES
//****************************************
#include <Wire.h>

//****************************************
//        DEFINES
//****************************************
#define MPU6050_ACCEL_XOUT_H       0x3B   // R
#define MPU6050_WHO_AM_I           0x75   // R
#define MPU6050_I2C_ADDRESS        0x68
#define MPU6050_PWR_MGMT_1         0x6B   // R/W
#define MPU6050_PWR_MGMT_2         0x6C   // R/W

#define AlarmTriggerPin D8

//USER SETTINGS
const unsigned long culAlarmDuration = 1000; //millis of beep duration
const unsigned long culCheckGyro = 300; //millis to check the Gyro position
const float AcMargin = 2.5; //all dimensions margin for the Accellerator in ??
const float GyMargin = 2.5; //all dimensions margin for the Gyro in degrees

//****************************************
//        TYPE DECLARATIONS
//****************************************
typedef union accel_t_gyro_union
{
  struct
  {
    uint8_t x_accel_h;
    uint8_t x_accel_l;
    uint8_t y_accel_h;
    uint8_t y_accel_l;
    uint8_t z_accel_h;
    uint8_t z_accel_l;
    uint8_t t_h;
    uint8_t t_l;
    uint8_t x_gyro_h;
    uint8_t x_gyro_l;
    uint8_t y_gyro_h;
    uint8_t y_gyro_l;
    uint8_t z_gyro_h;
    uint8_t z_gyro_l;
  } reg;
  struct
  {
    int16_t x_accel;
    int16_t y_accel;
    int16_t z_accel;
    int16_t temperature;
    int16_t x_gyro;
    int16_t y_gyro;
    int16_t z_gyro;
  } value;
};

//****************************************
//        GLOBALS
//****************************************
unsigned long ulLastGyroCheck = 0;
unsigned long ulAlarmEndMillis = 0;
bool fAlarmTriggered = false;

unsigned long last_read_time;
float         last_x_angle;  // These are the filtered angles
float         last_y_angle;
float         last_z_angle;
float         last_gyro_x_angle;  // Store the gyro angles to compare drift
float         last_gyro_y_angle;
float         last_gyro_z_angle;

//  Use the following global variables and corresponding access functions
//  to calibrate the acceleration sensor
float    base_x_accel;
float    base_y_accel;
float    base_z_accel;
float    base_x_gyro;
float    base_y_gyro;
float    base_z_gyro;

//****************************************
//        FUNCTION DECLARATIONS
//****************************************
void set_last_read_angle_data(unsigned long time, float x, float y, float z, float x_gyro, float y_gyro, float z_gyro);

inline unsigned long get_last_time() {return last_read_time;}
inline float get_last_x_angle() {return last_x_angle;}
inline float get_last_y_angle() {return last_y_angle;}
inline float get_last_z_angle() {return last_z_angle;}
inline float get_last_gyro_x_angle() {return last_gyro_x_angle;}
inline float get_last_gyro_y_angle() {return last_gyro_y_angle;}
inline float get_last_gyro_z_angle() {return last_gyro_z_angle;}
void calibrate_sensors();
int read_gyro_accel_raw_vals(uint8_t* accel_t_gyro_ptr);
void ReadAccGyrValues(void);

bool CheckAccMovementOutOfBound(float x, float y, float z);
bool CheckGyroMovementOutOfBound(float x, float y, float z);
void SetAlarmTrigger (bool fArm);
void convert_raw_2_real_values_and_store(unsigned long time_now,
                  float raw_gyro_x, float raw_gyro_y, float raw_gyro_z,
                  float raw_accel_x, float raw_accel_y, float raw_accel_z);

//****************************************
//        SETUP
//****************************************
void setup()
{
  int error;
  uint8_t c;

  Serial.begin(115200);
  Serial.println();
  Serial.println();

  // Initialize the 'Wire' class for the I2C-bus.
  Wire.begin();

  // default at power-up:
  //    Gyro at 250 degrees second
  //    Acceleration at 2g
  //    Clock source at internal 8MHz
  //    The device is in sleep mode.
  error = MPU6050_read (MPU6050_WHO_AM_I, &c, 1);

  Serial.print(F("error = "));
  Serial.println(error, DEC);

  // Serial.print(F("WHO_AM_I : "));
  // Serial.print(c,HEX);
  // Serial.print(F(", error = "));
  // Serial.println(error,DEC);


  // According to the datasheet, the 'sleep' bit
  // should read a '1'. But I read a '0'.
  // That bit has to be cleared, since the sensor
  // is in sleep mode at power-up. Even if the
  // bit reads '0'.
  error = MPU6050_read (MPU6050_PWR_MGMT_2, &c, 1);

  // Clear the 'sleep' bit to start the sensor.
  MPU6050_write_reg (MPU6050_PWR_MGMT_1, 0);

  //Initialize the angles
  calibrate_sensors();
  set_last_read_angle_data(millis(), 0, 0, 0, 0, 0, 0);

  convert_raw_2_real_values_and_store(0, base_x_gyro, base_y_gyro, base_z_gyro, base_x_accel, base_y_accel, base_z_accel);


  //two beeps
  SetAlarmTrigger (true, 500);
  delay(500);
  SetAlarmTrigger (false, 500);
  delay(500);
  SetAlarmTrigger (true, 500);
  delay(500);
  SetAlarmTrigger (false, 500);
  delay(1000);

}

//****************************************
//        LOOP
//****************************************
void loop()
{
  if ((ulLastGyroCheck + culCheckGyro <= millis()) && !fAlarmTriggered){
    //time to read the Gyro and Accellerator
      ReadAccGyrValues();

      bool fAccOutOfBound = CheckAccMovementOutOfBound(get_last_x_angle(), get_last_y_angle(), get_last_z_angle());
//      bool fGyroOutOfBound = CheckGyroMovementOutOfBound(rdNewState, GyMargin, GyMargin, GyMargin);

//      if (fAccOutOfBound || fGyroOutOfBound)
//      if (fAccOutOfBound)
//          SetAlarmTrigger(true, culAlarmDuration);

      ulLastGyroCheck = millis();
   }

  if (fAlarmTriggered && (ulAlarmEndMillis <= millis()))
     SetAlarmTrigger(false, 0);

}


//****************************************
//        functions
//****************************************
void ReadAccGyrValues(void)
{
  int error;
  double dT;
  accel_t_gyro_union accel_t_gyro;

  // Read the raw values.
  error = read_gyro_accel_raw_vals((uint8_t*) &accel_t_gyro);

  // Get the time of reading for rotation computations
  unsigned long t_now = millis();

  // float gyro_x = (accel_t_gyro.value.x_gyro - base_x_gyro);
  // float gyro_y = (accel_t_gyro.value.y_gyro - base_y_gyro);
  // float gyro_z = (accel_t_gyro.value.z_gyro - base_z_gyro);
  //
  // // Get raw acceleration values
  // //float G_CONVERT = 16384;
  // float accel_x = accel_t_gyro.value.x_accel;
  // float accel_y = accel_t_gyro.value.y_accel;
  // float accel_z = accel_t_gyro.value.z_accel;

  // Convert and update the saved data with the latest values
  convert_raw_2_real_values_and_store(t_now, accel_t_gyro.value.x_gyro, accel_t_gyro.value.y_gyro, accel_t_gyro.value.z_gyro,
                                        accel_t_gyro.value.x_accel, accel_t_gyro.value.y_accel, accel_t_gyro.value.z_accel);

};



void convert_raw_2_real_values_and_store(unsigned long time_now,
                  float raw_gyro_x, float raw_gyro_y, float raw_gyro_z,
                  float raw_accel_x, float raw_accel_y, float raw_accel_z)
{
  // Convert gyro values to degrees/sec
  float FS_SEL = 131;
  /*
  float gyro_x = (accel_t_gyro.value.x_gyro - base_x_gyro)/FS_SEL;
  float gyro_y = (accel_t_gyro.value.y_gyro - base_y_gyro)/FS_SEL;
  float gyro_z = (accel_t_gyro.value.z_gyro - base_z_gyro)/FS_SEL;
  */
  float gyro_x = (raw_gyro_x - base_x_gyro)/FS_SEL;
  float gyro_y = (raw_gyro_y - base_y_gyro)/FS_SEL;
  float gyro_z = (raw_gyro_z - base_z_gyro)/FS_SEL;


  // Get raw acceleration values
  //float G_CONVERT = 16384;
  float accel_x = raw_accel_x;
  float accel_y = raw_accel_y;
  float accel_z = raw_accel_z;

  // Get angle values from accelerometer
  float RADIANS_TO_DEGREES = 180/3.14159;
  float accel_vector_length = sqrt(pow(accel_x,2) + pow(accel_y,2) + pow(accel_z,2));
  float accel_angle_y = atan(-1*accel_x/sqrt(pow(accel_y,2) + pow(accel_z,2)))*RADIANS_TO_DEGREES;
  float accel_angle_x = atan(accel_y/sqrt(pow(accel_x,2) + pow(accel_z,2)))*RADIANS_TO_DEGREES;
  float accel_angle_z = 0;

  // Compute the (filtered) gyro angles
  float dt =(time_now - get_last_time())/1000.0;
  float gyro_angle_x = gyro_x*dt + get_last_x_angle();
  float gyro_angle_y = gyro_y*dt + get_last_y_angle();
  float gyro_angle_z = gyro_z*dt + get_last_z_angle();

  // Compute the drifting gyro angles
  float unfiltered_gyro_angle_x = gyro_x*dt + get_last_gyro_x_angle();
  float unfiltered_gyro_angle_y = gyro_y*dt + get_last_gyro_y_angle();
  float unfiltered_gyro_angle_z = gyro_z*dt + get_last_gyro_z_angle();

  // Apply the complementary filter to figure out the change in angle - choice of alpha is
  // estimated now.  Alpha depends on the sampling rate...
  float alpha = 0.96;
  float angle_x = alpha*gyro_angle_x + (1.0 - alpha)*accel_angle_x;
  float angle_y = alpha*gyro_angle_y + (1.0 - alpha)*accel_angle_y;
  float angle_z = gyro_angle_z;  //Accelerometer doesn't give z-angle

  // Update the saved data with the latest values
  set_last_read_angle_data(time_now, angle_x, angle_y, angle_z, unfiltered_gyro_angle_x, unfiltered_gyro_angle_y, unfiltered_gyro_angle_z);

  // Send the data to the serial port
  Serial.print("time|");Serial.print(time_now);
  Serial.print("| Acc|");Serial.print(angle_x);
  Serial.print("|");Serial.print(angle_y);
  Serial.print("|");Serial.print(angle_z);
  Serial.print("| GyroFilt|");Serial.print(gyro_angle_x);
  Serial.print("|");Serial.print(gyro_angle_y);
  Serial.print("|");Serial.print(gyro_angle_z);
  Serial.print("| GyroUnfilt|");Serial.print(unfiltered_gyro_angle_x);
  Serial.print("|");Serial.print(unfiltered_gyro_angle_y);
  Serial.print("|");Serial.print(unfiltered_gyro_angle_z);
  Serial.print("| Vector|");Serial.println(accel_vector_length);


}


// --------------------------------------------------------
// MPU6050_read
//
// This is a common function to read multiple bytes
// from an I2C device.
//
// It uses the boolean parameter for Wire.endTransMission()
// to be able to hold or release the I2C-bus.
// This is implemented in Arduino 1.0.1.
//
// Only this function is used to read.
// There is no function for a single byte.
//
int MPU6050_read(int start, uint8_t *buffer, int size)
{
  int i, n, error;

  Wire.beginTransmission(MPU6050_I2C_ADDRESS);
  n = Wire.write(start);
  if (n != 1)
    return (-10);

  n = Wire.endTransmission(false);    // hold the I2C-bus
  if (n != 0)
    return (n);

  // Third parameter is true: relase I2C-bus after data is read.
  Wire.requestFrom(MPU6050_I2C_ADDRESS, size, true);
  i = 0;
  while(Wire.available() && i<size)
  {
    buffer[i++]=Wire.read();
  }
  if ( i != size)
    return (-11);

  return (0);  // return : no error
}


void set_last_read_angle_data(unsigned long time, float x, float y, float z, float x_gyro, float y_gyro, float z_gyro) {
  last_read_time = time;
  last_x_angle = x;
  last_y_angle = y;
  last_z_angle = z;
  last_gyro_x_angle = x_gyro;
  last_gyro_y_angle = y_gyro;
  last_gyro_z_angle = z_gyro;
}

// --------------------------------------------------------
// MPU6050_write
//
// This is a common function to write multiple bytes to an I2C device.
//
// If only a single register is written,
// use the function MPU_6050_write_reg().
//
// Parameters:
//   start : Start address, use a define for the register
//   pData : A pointer to the data to write.
//   size  : The number of bytes to write.
//
// If only a single register is written, a pointer
// to the data has to be used, and the size is
// a single byte:
//   int data = 0;        // the data to write
//   MPU6050_write (MPU6050_PWR_MGMT_1, &c, 1);
//
int MPU6050_write(int start, const uint8_t *pData, int size)
{
  int n, error;

  Wire.beginTransmission(MPU6050_I2C_ADDRESS);
  n = Wire.write(start);        // write the start address
  if (n != 1)
    return (-20);

  n = Wire.write(pData, size);  // write data bytes
  if (n != size)
    return (-21);

  error = Wire.endTransmission(true); // release the I2C-bus
  if (error != 0)
    return (error);

  return (0);         // return : no error
}

// --------------------------------------------------------
// MPU6050_write_reg
//
// An extra function to write a single register.
// It is just a wrapper around the MPU_6050_write()
// function, and it is only a convenient function
// to make it easier to write a single register.
//
int MPU6050_write_reg(int reg, uint8_t data)
{
  int error;

  error = MPU6050_write(reg, &data, 1);

  return (error);
}



int read_gyro_accel_raw_vals(uint8_t* accel_t_gyro_ptr) {
  // Read the raw values.
  // Read 14 bytes at once,
  // containing acceleration, temperature and gyro.
  // With the default settings of the MPU-6050,
  // there is no filter enabled, and the values
  // are not very stable.  Returns the error value

  accel_t_gyro_union* accel_t_gyro = (accel_t_gyro_union *) accel_t_gyro_ptr;

  int error = MPU6050_read (MPU6050_ACCEL_XOUT_H, (uint8_t *) accel_t_gyro, sizeof(*accel_t_gyro));

  // Swap all high and low bytes.
  // After this, the registers values are swapped,
  // so the structure name like x_accel_l does no
  // longer contain the lower byte.
  uint8_t swap;
  #define SWAP(x,y) swap = x; x = y; y = swap

  SWAP ((*accel_t_gyro).reg.x_accel_h, (*accel_t_gyro).reg.x_accel_l);
  SWAP ((*accel_t_gyro).reg.y_accel_h, (*accel_t_gyro).reg.y_accel_l);
  SWAP ((*accel_t_gyro).reg.z_accel_h, (*accel_t_gyro).reg.z_accel_l);
  SWAP ((*accel_t_gyro).reg.t_h, (*accel_t_gyro).reg.t_l);
  SWAP ((*accel_t_gyro).reg.x_gyro_h, (*accel_t_gyro).reg.x_gyro_l);
  SWAP ((*accel_t_gyro).reg.y_gyro_h, (*accel_t_gyro).reg.y_gyro_l);
  SWAP ((*accel_t_gyro).reg.z_gyro_h, (*accel_t_gyro).reg.z_gyro_l);

  return error;
}

// The sensor should be motionless on a horizontal surface
//  while calibration is happening
void calibrate_sensors() {
  int                   num_readings = 10;
  float                 x_accel = 0;
  float                 y_accel = 0;
  float                 z_accel = 0;
  float                 x_gyro = 0;
  float                 y_gyro = 0;
  float                 z_gyro = 0;
  accel_t_gyro_union    accel_t_gyro;

  Serial.println("Starting Calibration");

  // Discard the first set of values read from the IMU
  read_gyro_accel_raw_vals((uint8_t *) &accel_t_gyro);

  // Read and average the raw values from the IMU
  for (int i = 0; i < num_readings; i++) {
    read_gyro_accel_raw_vals((uint8_t *) &accel_t_gyro);
    x_accel += accel_t_gyro.value.x_accel;
    y_accel += accel_t_gyro.value.y_accel;
    z_accel += accel_t_gyro.value.z_accel;
    x_gyro += accel_t_gyro.value.x_gyro;
    y_gyro += accel_t_gyro.value.y_gyro;
    z_gyro += accel_t_gyro.value.z_gyro;
    delay(100);
  }
  x_accel /= num_readings;
  y_accel /= num_readings;
  z_accel /= num_readings;
  x_gyro /= num_readings;
  y_gyro /= num_readings;
  z_gyro /= num_readings;

  // Store the raw calibration values globally
  base_x_accel = x_accel;
  base_y_accel = y_accel;
  base_z_accel = z_accel;
  base_x_gyro = x_gyro;
  base_y_gyro = y_gyro;
  base_z_gyro = z_gyro;

  Serial.println(F("Finished Calibration"));
}


// KLADJE!!!

bool CheckAccMovementOutOfBound(float x, float y, float z){
  //built for speed
  //Will be within bounds most of the times - inner cube: 0.5 * sqrt(2) = 0.71
  float dX = abs(x - base_x_accel);
  float dY = abs(y - base_y_accel);
  float dZ = abs(z - base_z_accel);

  float radius = sqrt(sq(dX) + sq(dY) + sq(dZ));
/*
  Serial.print(millis());
  Serial.print(" Raduis, ");
  Serial.print(x,4);
  Serial.print(" , ");
  Serial.print(y,4);
  Serial.print(" , ");
  Serial.print(z,4);
  Serial.print(" | ");
  Serial.print(dX,4);
  Serial.print(" , ");
  Serial.print(dY,4);
  Serial.print(" , ");
  Serial.print(dZ,4);
  Serial.print(" | ");
    Serial.println(radius,4);
*/
  return (radius <= AcMargin);
};

// bool CheckGyroMovementOutOfBound(scaleddata State, float Xmargin, float Ymargin,float Zmargin){
//
//   bool fX = abs(State.GyX - RestState.GyX) > Xmargin;
//   bool fY = abs(State.GyY - RestState.GyY) > Ymargin;
//   bool fZ = abs(State.GyZ - RestState.GyZ) > Zmargin;
//
//   return (fX || fY || fZ);
//
// };

void SetAlarmTrigger (bool fArm, unsigned long ulDuration){

  if (fArm){  //switch ON AlarmTriggerPin
    analogWriteRange(1000);
    analogWrite(AlarmTriggerPin, 512);
    ulAlarmEndMillis = millis() + ulDuration;
    fAlarmTriggered = true;
  }
  else
  { //switch OFF AlarmTriggerPin
    analogWrite(AlarmTriggerPin, 0);
    pinMode(AlarmTriggerPin, OUTPUT);
    digitalWrite(AlarmTriggerPin, LOW);
    fAlarmTriggered = false;
  }

};
